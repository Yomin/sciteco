#!./sciteco -m

! TECO.INI !

@0{
  Oadd,edit,close,quit

!add!
  EQ* HX.f EQ.f

  @^U.c{
    U.c ((Q.c&1)*255) # ((Q.c/2 & 1)*255*256) # ((Q.c/4 & 1)*255*256*256)
  }

  ! <bold,bg,fg,style>M.r !
  @.r{
    U.sU.vU.bU.h
    Q.v:M.c,Q.s,2051ES
    Q.b:M.c,Q.s,2052ES
    Q.h,Q.s,2053ES
  }

  ZJ -:S.[cpp,c,h]"S Z-."=
    EBQ.f
    3,4001ES
    0,4005ESand and_eq asm auto bitand bitor bool break
            case catch char class compl const const_cast continue
            default delete do double dynamic_cast else enum explicit export extern false float for
            friend goto if inline int long mutable namespace new not not_eq
            operator or or_eq private protected public
            register reinterpret_cast return short signed sizeof static static_cast struct switch
            template this throw true try typedef typeid typename union unsigned using
            virtual void volatile wchar_t while xor xor_eq
    2,4005ESa addindex addtogroup anchor arg attention
            author b brief bug c class code date def defgroup deprecated dontinclude
            e em endcode endhtmlonly endif endlatexonly endlink endverbatim enum example exception
            f$ f[ f] file fn hideinitializer htmlinclude htmlonly
            if image include ingroup internal invariant interface latexonly li line link
            mainpage name namespace nosubgrouping note overload
            p page par param post pre ref relates remarks return retval
            sa section see showinitializer since skip skipline struct subsection
            test throw todo typedef union until
            var verbatim verbinclude version warning weakgroup $ @ \ & < > # { }
    ! Whitespace   ! 0,0,2,0:M.r
    ! Comments     ! 0,0,2,1:M.r 0,0,2,2:M.r 1,0,2,3:M.r
    ! Number       ! 0,0,1,4:M.r
    ! Keywords     ! 0,0,4,5:M.r
    ! Strings      ! 0,0,5,6:M.r 1,0,5,7:M.r
    ! Preprocessor ! 0,0,3,9:M.r
    ! Operators    ! 1,0,7,10:M.r
    ! Identifiers  ! 0,0,6,11:M.r
    Oend
  ''

  ZJ -:Smakefile"S Z-."=
    EBQ.f
    11,4001ES
    0,4005ESifeq else endif
    ! Default      ! 0,0,7,0:M.r
    ! Comments     ! 0,0,2,1:M.r
    ! Preprocessor ! 0,0,4,2:M.r
    ! Variables    ! 0,0,1,3:M.r
    ! Operators    ! 1,0,7,4:M.r
    ! Target       ! 1,0,2,5:M.r
    Oend
  ''

  EBQ.f
  Oend

!edit!
  Oend

!close!
  Oend

!quit!
  ! fall through !

!end!}
ED#32ED

! open all files specified on the commandline !
[.f
  <:L;R 0X.f EBQ.f EB L>
].f
-EF
